apply from: 'dependencies.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

subprojects {
    apply plugin: 'java'

    group = project.groupId

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://teavm.org/maven/repository/" }
    }

    configurations.configureEach {
        // Check for updates every build
        resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
    }
}

var libProjects = [
        project(":imgui:imgui-core:imgui-core"),
        project(":imgui:imgui-core:imgui-core-teavm"),
        project(":imgui:imgui-extensions:imgui-gdx"),
        project(":imgui:imgui-extensions:imgui-gdx-lwjgl3"),
        project(":imgui:imgui-extensions:imgui-gdx-frame-viewport"),
        project(":imgui:imgui-extensions:imgui-imlayout:imgui-imlayout")
]

configure(libProjects) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        repositories {
            maven {
                url {
                    def ver = project.version.toString()
                    def isSnapshot = ver.toUpperCase().contains("SNAPSHOT")
                    def repoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def repoUrlSnapshot = "https://oss.sonatype.org/content/repositories/snapshots/"
                    isSnapshot ? repoUrlSnapshot : repoUrl
                }
                credentials {
                    username = System.getenv('USER')
                    password = System.getenv('PASSWORD')
                }
            }
        }
    }

    javadoc {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing.publications.configureEach { mav ->
        pom {
            name = "jDear-imgui"
            description = "Java JNI based binding for imgui"
            url = "https://github.com/xpenatan/jDear-imgui"
            developers {
                developer {
                    id = "Xpe"
                    name = "Natan"
                }
            }
            scm {
                connection = "scm:git:git://https://github.com/xpenatan/jDear-imgui.git"
                developerConnection = "scm:git:ssh://https://github.com/xpenatan/jDear-imgui.git"
                url = "http://https://github.com/xpenatan/jDear-imgui/tree/master"
            }
            licenses {
                license {
                    name = "The Apache License, Version 2.0"
                    url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                }
            }
        }
    }

    signing {
        def signingKey = System.getenv("SIGNING_KEY")
        def signingPassword = System.getenv("SIGNING_PASSWORD")
        useInMemoryPgpKeys(signingKey, signingPassword)
        publishing.publications.configureEach { mav ->
            sign mav.name
        }
    }
}

tasks.register('buildCore') {
    dependsOn ":imgui:imgui-core:imgui-core-build:generateNatives"
}

tasks.register('buildCpp') {
    dependsOn ":imgui:imgui-cpp:imgui-cpp:generateNatives"
}

tasks.register('buildImLayout') {
    dependsOn ":imgui:imgui-extensions:imgui-imlayout:imgui-imlayout-build:generateNatives"
}

tasks.register('buildImGuiNatives') {
    group = 'imgui'
    dependsOn buildCore, buildCpp, buildImLayout
}

buildCore.shouldRunAfter buildCpp

buildImLayout.shouldRunAfter buildCore
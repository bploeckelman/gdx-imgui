plugins {
    id 'java'
}

group = project.libGroup

def module_name = "core"

configurations {
    includeDep
    implementation.extendsFrom includeDep
}

dependencies {
    includeDep "com.github.xpenatan.jParser:jParser-loader:$project.jParserVersion"

    testImplementation files(".jni/libs/imgui-core-natives.jar")
    testImplementation "junit:junit:$project.jUnitVersion"
}

clean.doFirst {
    def jniPath = projectDir.toString() + "/jni/"
    def srcPath = projectDir.toString() + "/src/main/"

    if(new File(jniPath).exists()) {
        project.delete(files(jniPath))
    }
    if(new File(srcPath).exists()) {
        project.delete(files(srcPath))
    }
}

task platformAll(type: Jar) {
    archiveClassifier.set("natives-desktop")
    from zipTree(projectDir.toString() + "/jni/libs/imgui-core-natives.jar")
}

task fromClasses(type: Jar) {
    from(sourceSets.main.output) {
    }
}

publishing {
    publications {
        imguiClasses(MavenPublication) {
            version project.jDearImguiVersion
            project.version = project.jDearImguiVersion
            artifactId module_name
            artifact fromClasses

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.includeDep.allDependencies.each {
                    def dependencyNode = dependencies.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
        imguiNatives(MavenPublication) {
            version project.jDearImguiVersion
            project.version = project.jDearImguiVersion
            artifactId module_name + "-platform"
            artifact platformAll
        }
    }
}
